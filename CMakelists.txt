cmake_minimum_required(VERSION 4.0)
project(learn_vulkan LANGUAGES CXX VERSION 1.0.0)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/target/bin)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/target/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/target/lib)

add_subdirectory(ext)
add_subdirectory(src)

set(SHADER_SOURCE_DIR "${PROJECT_SOURCE_DIR}/shaders")
set(SHADER_BINARY_DIR "${PROJECT_SOURCE_DIR}/target/shaders")

add_custom_command(
    OUTPUT
        ${SHADER_SOURCE_DIR}/vert.spv
        ${SHADER_SOURCE_DIR}/frag.spv
    COMMAND ${SHADER_SOURCE_DIR}/compile.sh
    WORKING_DIRECTORY ${SHADER_SOURCE_DIR}
    DEPENDS
        ${SHADER_SOURCE_DIR}/shader.vert
        ${SHADER_SOURCE_DIR}/shader.frag
        ${SHADER_SOURCE_DIR}/compile.sh
    COMMENT "Compiling shaders"
)

add_custom_target(compile_shaders ALL
    DEPENDS
        ${SHADER_SOURCE_DIR}/vert.spv
        ${SHADER_SOURCE_DIR}/frag.spv
)

add_custom_target(copy_shaders ALL
    COMMAND ${CMAKE_COMMAND} -E make_directory ${SHADER_BINARY_DIR}
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        ${SHADER_SOURCE_DIR}/vert.spv
        ${SHADER_BINARY_DIR}/vert.spv
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        ${SHADER_SOURCE_DIR}/frag.spv
        ${SHADER_BINARY_DIR}/frag.spv
    DEPENDS
        compile_shaders
    COMMENT "Copying shader files to build directory"
)

set(TEXTURE_SOURCE_DIR "${PROJECT_SOURCE_DIR}/textures")
set(TEXTURE_BINARY_DIR "${PROJECT_SOURCE_DIR}/target/textures")
add_custom_target(copy_textures ALL
    COMMAND ${CMAKE_COMMAND} -E make_directory ${TEXTURE_BINARY_DIR}
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
        ${TEXTURE_SOURCE_DIR}/texture.png
        ${TEXTURE_BINARY_DIR}/texture.png
    DEPENDS 
        ${TEXTURE_SOURCE_DIR}/texture.png
    COMMENT "Copying texture files to build directory"
)